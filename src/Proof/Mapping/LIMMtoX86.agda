{-# OPTIONS --safe #-}

open import Arch.General.Execution
open import Arch.X86
open import MapLIMMtoX86


module Proof.Mapping.LIMMtoX86
  -- Given a target execution
  (dst : Execution LabelX86)
  -- which is wellformed
  (dst-wf : WellFormed dst)
  -- and generated by a program produced through the mapping
  (dst-ok : X86-LIMMRestricted dst)
  where

-- Stdlib imports
open import Data.Product using (_×_; _,_; ∃-syntax)
-- Library imports
open import Dodo.Binary
-- Local imports: Architectures
open import Arch.LIMM
-- Local imports: Proofs
open import Proof.Mapping.LIMMtoX86.Execution dst dst-wf dst-ok
open import Proof.Mapping.LIMMtoX86.Consistent dst dst-wf dst-ok
open import Proof.Mapping.LIMMtoX86.Mapping dst dst-wf dst-ok
import Proof.Mapping.Framework LabelLIMM dst dst-wf as Δ


open X86-LIMMRestricted
open IsX86Consistent
open Δ.Definitions δ
open Δ.WellFormed δ
open Δ.Behavior δ


proof-LIMM⇒X86 :
  -- We produce a source execution
  ∃[ src ]
    ( -- which is wellformed,
      WellFormed src
    × -- consistent in the source architecture,
      IsLIMMConsistent src
    × -- related to the target by the mapping on instructions,
      LIMM⇒X86 src dst
    × -- and has identical behavior to the target
      behavior src ⇔₂ behavior dst
    )
proof-LIMM⇒X86 =
  ( src
  , src-wf
  , src-consistent
  , src-mapping
  , proof-behavior
  )
