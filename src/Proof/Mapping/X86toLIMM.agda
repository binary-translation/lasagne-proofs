{-# OPTIONS --safe #-}

open import Arch.General.Execution
open import Arch.LIMM
open import MapX86toLIMM


module Proof.Mapping.X86toLIMM
  -- Given a target execution
  (dst : Execution LabelLIMM)
  -- which is wellformed
  (dst-wf : WellFormed dst)
  -- and generated by a program produced through the mapping
  (dst-ok : LIMM-X86Restricted dst)
  where

-- Stdlib imports
open import Data.Product using (_×_; _,_; ∃-syntax)
-- Library imports
open import Dodo.Binary
-- Local imports: Architectures
open import Arch.X86
-- Local imports: Proofs
open import Proof.Mapping.X86toLIMM.Execution dst dst-wf dst-ok
open import Proof.Mapping.X86toLIMM.Consistent dst dst-wf dst-ok
open import Proof.Mapping.X86toLIMM.Mapping dst dst-wf dst-ok
import Proof.Mapping.Framework LabelX86 dst dst-wf as Δ


open LIMM-X86Restricted
open IsLIMMConsistent
open Δ.Definitions δ
open Δ.WellFormed δ
open Δ.Behavior δ


proof-X86⇒LIMM :
  -- We produce a source execution
  ∃[ src ]
    ( -- which is wellformed,
      WellFormed src
    × -- consistent in the source architecture,
      IsX86Consistent src
    × -- related to the target by the mapping on instructions,
      X86⇒LIMM src dst
    × -- and has identical behavior to the target
      behavior src ⇔₂ behavior dst
    )
proof-X86⇒LIMM =
  ( src
  , src-wf
  , src-consistent
  , src-mapping
  , proof-behavior
  )
